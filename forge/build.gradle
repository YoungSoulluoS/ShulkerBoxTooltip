plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id 'com.modrinth.minotaur' version '2.+'
}

archivesBaseName = "${rootProject.projectArchiveBaseName}-forge"

architectury {
    platformSetupLoomIde()
    forge()
}

loom {
    accessWidenerPath = project(':common').loom.accessWidenerPath

    forge {
        accessTransformers = files('src/main/resources/META-INF/accesstransformer.cfg')
        mixinConfig 'shulkerboxtooltip-common.mixins.json'
        mixinConfig 'shulkerboxtooltip-forge.mixins.json'
    }
}

configurations {
    common
    shadowCommon
    // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

dependencies {
    forge "net.minecraftforge:forge:${rootProject.forgeVersion}"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowCommon(project(path: ':common', configuration: 'transformProductionForge')) { transitive = false }

    // Nullable annotations.
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation 'org.jetbrains:annotations:23.0.0'

    // Jankson
    localRuntime("blue.endless:jankson:${rootProject.janksonVersion}")
    forgeRuntimeLibrary(shadowCommon("blue.endless:jankson:${rootProject.janksonVersion}"))
}

processResources {
    inputs.property "version", project.version

    filesMatching('META-INF/mods.toml') {
        expand 'version': project.version
    }
}

shadowJar {
    exclude 'fabric.mod.json'
    exclude 'architectury.common.json'

    relocate 'blue.endless.jankson', "${rootProject.group}.shulkerboxtooltip.shadowed.blue.endless.jankson"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier.set('dev-shadow')
}

remapJar {
    inputFile = shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier.set(null)
}

// magical bit of config to get Forge somewhat working
tasks.withType(net.fabricmc.loom.task.AbstractRemapJarTask).configureEach {
    targetNamespace = "named"
}
configurations.all {
    resolutionStrategy {
        force 'net.sf.jopt-simple:jopt-simple:5.0.4'
    }
}

jar {
    archiveClassifier.set('dev')
}

sourcesJar {
    def commonSources = project(':common').sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

// Include both common and forge documentation in the javadoc
def javadocProjects = [':forge', ':common']
javadoc {
    source = javadocProjects.collect { project(it).sourceSets.main.allJava }
    classpath = files(javadocProjects.collect { project(it).sourceSets.main.compileClasspath })

    exclude 'com/misterpemodder/shulkerboxtooltip/impl/**'
    exclude 'com/misterpemodder/shulkerboxtooltip/mixin/**'
    exclude 'com/misterpemodder/shulkerboxtooltip/*.java'
    exclude 'com/misterpemodder/shulkerboxtooltip/forge/*.java'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenForge(MavenPublication) {
            groupId = project.group
            artifactId = "${project.archivesBaseName}"
            version = project.version

            artifact(remapJar) {
                builtBy remapJar
            }

            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }

            artifact(javadocJar) {
                builtBy javadocJar
            }

            pom rootProject.ext.makePom(project)
        }
    }
}

signing {
    def signingKey = findProperty('signingKey')
    def signingPassword = findProperty('signingPassword')
    if (signingKey && signingPassword) {
        useInMemoryPgpKeys(signingKey, signingPassword)
        sign publishing.publications.mavenForge
    } else {
        println("${project.name}: skipping jar signing")
    }
}
tasks.curseforge.enabled = System.getenv('CURSE_API_KEY') && project.hasProperty('curseProjectId')

curseforge {
    if (System.getenv('CURSE_API_KEY'))
        apiKey = System.getenv('CURSE_API_KEY')

    project {
        id = project.curseProjectId
        changelog = rootProject.parseChangelog('changelog.txt')
        releaseType = rootProject.getReleaseType()
        for (gameVersion in project.hostGameVersions.split(',')) {
            addGameVersion gameVersion
        }
        addGameVersion 'Forge'
        addGameVersion 'Java 21'
        mainArtifact(remapJar) {
            displayName = "[Forge $project.mcVersionFull] v${project.version}"
        }
    }
    options {
        forgeGradleIntegration = false
        javaVersionAutoDetect = false
        debug = project.projectBaseVersion.contains('SNAPSHOT')
    }
}

afterEvaluate {
    // Manually set dependsOn because curseforge doesn't set one.
    tasks."curseforge${project.curseProjectId}".dependsOn remapJar
}

tasks.modrinth.enabled = System.getenv('MODRINTH_TOKEN') && project.hasProperty('modrinthProjectId')

modrinth {
    token = System.getenv('MODRINTH_TOKEN')
    projectId = modrinthProjectId
    versionNumber = "${project.version}-forge"
    versionName = "[Forge $project.mcVersionFull] v${project.version}"
    versionType = project.getReleaseType()
    changelog = rootProject.parseChangelog('changelog.txt')
    uploadFile = remapJar
    gameVersions = hostGameVersions.split(',').toList()
    loaders = ['forge']
    debugMode = project.projectBaseVersion.contains('SNAPSHOT')
}
